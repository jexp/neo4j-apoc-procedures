[[load-json]]
= Load JSON

[abstract]
--
This section describes procedures that can be used to import JSON data from web APIs or files.
--

Web APIs are a huge opportunity to access and integrate data from any sources with your graph.
Most of them provide the data is JSON format.

The Load JSON procedures retrieve data from URLs or maps and turn it into map value(s) for Cypher to consume.
Cypher has support for deconstructing nested documents with dot syntax, slices, `UNWIND` etc. so it is easy to turn nested data into graphs.

Sources with multiple JSON objects (JSONL,JSON Lines) in a stream, like the https://dev.twitter.com/streaming/overview/processing[streaming Twitter format] or the Yelp Kaggle dataset, are also supported,

ifdef::backend-html5[]
++++
<iframe width="560" height="315" src="https://www.youtube.com/embed/M1P1IlQdb5M" frameborder="0" allow="autoplay; encrypted-media" allowfullscreen></iframe>
++++
endif::[]

This section includes:

* <<load-json-available-procedures>>
* <<load-json-json-path>>
* <<load-json-examples>>
    ** <<load-json-examples-stackoverflow>>
    ** <<load-json-examples-twitter>>
    ** <<load-json-examples-import-json>>
    ** <<load-json-examples-post-json>>

[[load-json-available-procedures]]
== Available Procedures

The table below describes the available procedures:

[separator=¦,opts=header,cols="1,1m,1m,5"]
|===
include::../../../build/generated-documentation/apoc.load.json.csv[]
include::../../../build/generated-documentation/apoc.load.jsonParams.csv[lines=2:]
include::../../../build/generated-documentation/apoc.load.jsonArray.csv[lines=2:]
include::../../../build/generated-documentation/apoc.import.json.csv[lines=2:]
|===


[[load-json-json-path]]
== JSON-Path

Most of the `apoc.load.json` and `apoc.convert.*Json` procedures and functions now accept a json-path as last argument.

The json-path uses the https://github.com/jayway/JsonPath#operators[Java implementation by Jayway] of http://goessner.net/articles/JsonPath/[Stefan Gössners JSON-Path]

Here is some syntax, there are more examples at the links above.

`$.store.book[0].title`

.Operators
[options="header",cols="2m,4a"]
|===
| Operator                 | Description
| $                        | The root element to query. This starts all path expressions.
| @                        | The current node being processed by a filter predicate.
| *                        | Wildcard. Available anywhere a name or numeric are required.
| ..                       | Deep scan. Available anywhere a name is required.
| .<name>                  | Dot-notated child
| ['<name>' (,'<name>')]  | Bracket-notated child or children
| [<number> (,<number>)]  | Array index or indexes
| [start:end]              | Array slice operator
| [?(<expression>)]        | Filter expression. Expression must evaluate to a boolean value.
|===

If used, this path is applied to the json and can be used to extract sub-documents and -values before handing the result to Cypher, resulting in shorter statements with complex nested JSON.

There is also a direct `apoc.json.path(json,path)` function.

To simplify the JSON URL syntax, you can configure aliases in `conf/apoc.conf`:

----
apoc.json.myJson.url=https://api.stackexchange.com/2.2/questions?pagesize=100&order=desc&sort=creation&tagged=neo4j&site=stackoverflow&filter=!5-i6Zw8Y)4W7vpy91PMYsKM-k9yzEsSC1_Uxlf
----

----
CALL apoc.load.json('https://api.stackexchange.com/2.2/questions?pagesize=100&order=desc&sort=creation&tagged=neo4j&site=stackoverflow&filter=!5-i6Zw8Y)4W7vpy91PMYsKM-k9yzEsSC1_Uxlf')

becomes

CALL apoc.load.json('myJson')
----

The 3rd value in the `apoc.json.<alias>.url=` effectively defines an alias to be used in  `apoc.load.json('<alias>',....`

[[load-json-examples]]
== Examples

[[load-json-examples-stackoverflow]]
=== Import from StackOverflow API

`apoc.load.json` enables loading JSON data from any file or URL.
If the result is a JSON object, it is returned as a singular map.
If the result is an array, it is turned into a stream of maps.

StackOverflow provides several APIs, including one for retrieving recent questions and answers.
The URL for retrieving the last questions and answers for the http://stackoverflow.com/questions/tagged/neo4j[neo4j tag^] is:

[source,text]
----
https://api.stackexchange.com/2.2/questions?pagesize=100&order=desc&sort=creation&tagged=neo4j&site=stackoverflow&filter=!5-i6Zw8Y)4W7vpy91PMYsKM-k9yzEsSC1_Uxlf
----

Let's introspect the data that is returned from this end point.

.The following finds the 5 most recent questions with the `neo4j` tag on StackOverflow
[source,cypher]
----
WITH "https://api.stackexchange.com/2.2/questions?pagesize=100&order=desc&sort=creation&tagged=neo4j&site=stackoverflow&filter=!5-i6Zw8Y)4W7vpy91PMYsKM-k9yzEsSC1_Uxlf" AS url
CALL apoc.load.json(url) YIELD value
UNWIND value.items AS item
RETURN item.title, item.owner, item.creation_date, keys(item)
LIMIT 5;
----

.Results
[options="header", cols="2,3,2,2"]
|===
| item.title                                                         | item.owner                                                                                                                                                                                                                                                                                                    | item.creation_date | keys(item)
| "Cypher patten for getting self related nodes"                     | {profile_image: "https://lh3.googleusercontent.com/-1FWbhuaEBiQ/AAAAAAAAAAI/AAAAAAAAAIA/tLM_mEb-8MY/photo.jpg?sz=128", user_type: "registered", user_id: 5730203, link: "https://stackoverflow.com/users/5730203/asif-ali", reputation: 1148, display_name: "Asif Ali", accept_rate: 90}                      | 1586944991         | ["owner", "comment_count", "link", "last_activity_date", "creation_date", "answer_count", "title", "question_id", "tags", "share_link", "score", "down_vote_count", "body_markdown", "favorite_count", "is_answered", "delete_vote_count", "close_vote_count", "view_count", "up_vote_count"]
| "Problem connecting .NET Client to Neo4j Desktop version 4"        | {profile_image: "https://www.gravatar.com/avatar/a3fac35d600d1d462d8fc12f3926074c?s=128&d=identicon&r=PG&f=1", user_type: "registered", user_id: 2853912, link: "https://stackoverflow.com/users/2853912/user2853912", reputation: 21, display_name: "user2853912"}                                           | 1586938954         | ["owner", "comment_count", "link", "last_activity_date", "creation_date", "answer_count", "title", "question_id", "tags", "share_link", "score", "down_vote_count", "body_markdown", "favorite_count", "is_answered", "delete_vote_count", "close_vote_count", "view_count", "up_vote_count"]
| "What kind of graph algorithm does Neo4j use?"                     | {profile_image: "https://www.gravatar.com/avatar/736024b862a229111d4b3119875753b0?s=128&d=identicon&r=PG&f=1", user_type: "registered", user_id: 4402081, link: "https://stackoverflow.com/users/4402081/mariappan", reputation: 7, display_name: "Mariappan"}                                                | 1586901300         | ["owner", "comment_count", "answers", "link", "last_activity_date", "creation_date", "answer_count", "title", "question_id", "tags", "share_link", "score", "down_vote_count", "body_markdown", "favorite_count", "is_answered", "delete_vote_count", "close_vote_count", "view_count", "up_vote_count"]
| "Import json file to Neo4j"                                        | {profile_image: "https://lh3.googleusercontent.com/-PWDC85Kp2ig/AAAAAAAAAAI/AAAAAAAAAAA/AB6qoq3nhmVZl-_0VDKESOG5MsyHvXnw_A/mo/photo.jpg?sz=128", user_type: "registered", user_id: 9964138, link: "https://stackoverflow.com/users/9964138/jo%c3%a3o-costa", reputation: 23, display_name: "Jo&#227;o Costa"} | 1586897574         | ["owner", "comment_count", "answers", "link", "last_activity_date", "creation_date", "answer_count", "title", "question_id", "tags", "share_link", "score", "down_vote_count", "body_markdown", "favorite_count", "is_answered", "delete_vote_count", "close_vote_count", "view_count", "up_vote_count"]
| "Difference between Neo4j Graph Algorithms and Graph Data Science" | {profile_image: "https://i.stack.imgur.com/2rLPZ.jpg?s=128&g=1", user_type: "registered", user_id: 3297954, link: "https://stackoverflow.com/users/3297954/rotten", reputation: 1295, display_name: "rotten", accept_rate: 75}                                                                                | 1586872077         | ["owner", "comment_count", "answers", "link", "last_activity_date", "creation_date", "answer_count", "title", "question_id", "tags", "share_link", "score", "down_vote_count", "body_markdown", "favorite_count", "is_answered", "delete_vote_count", "close_vote_count", "view_count", "up_vote_count"]
|===

.The following finds StackOverflow questions and authors using JSON-path
[source,cypher]
----
WITH "https://api.stackexchange.com/2.2/questions?pagesize=100&order=desc&sort=creation&tagged=neo4j&site=stackoverflow&filter=!5-i6Zw8Y)4W7vpy91PMYsKM-k9yzEsSC1_Uxlf" AS url
CALL apoc.load.json(url,'$.items.owner.name') YIELD value
RETURN name, count(*);
----

Let's now create a Neo4j graph based on those entities.

.The following creates a graph based on data from the StackOverflow API
[source,cypher]
----
WITH "https://api.stackexchange.com/2.2/questions?pagesize=100&order=desc&sort=creation&tagged=neo4j&site=stackoverflow&filter=!5-i6Zw8Y)4W7vpy91PMYsKM-k9yzEsSC1_Uxlf" AS url
CALL apoc.load.json(url) YIELD value
UNWIND value.items AS q
MERGE (question:Question {id:q.question_id})
ON CREATE SET question.title = q.title,
              question.share_link = q.share_link,
              question.favorite_count = q.favorite_count

FOREACH (tagName IN q.tags | MERGE (tag:Tag {name:tagName}) MERGE (question)-[:TAGGED]->(tag))
FOREACH (a IN q.answers |
   MERGE (question)<-[:ANSWERS]-(answer:Answer {id:a.answer_id})
   MERGE (answerer:User {id:a.owner.user_id}) ON CREATE SET answerer.display_name = a.owner.display_name
   MERGE (answer)<-[:PROVIDED]-(answerer)
)

WITH * WHERE NOT q.owner.user_id IS NULL
MERGE (owner:User {id:q.owner.user_id}) ON CREATE SET owner.display_name = q.owner.display_name
MERGE (owner)-[:ASKED]->(question)
----

The Neo4j Browser visualization below shows the imported graph:

image::apoc-load-json-so.svg[width="1000px"]

[[load-json-examples-twitter]]
=== Import from Twitter API (with additional parameters)

With `apoc.load.jsonParams` we can send additional headers or payload with our JSON GET request, e.g. for the Twitter API:

Let's first configure the Bearer and Twitter Search URL token in `apoc.conf`:

.apoc.conf
----
apoc.static.twitter.bearer=XXXX
apoc.static.twitter.url=https://api.twitter.com/1.1/search/tweets.json?count=100&result_type=recent&lang=en&q=
----

These values can then be retrieved using the functions described in <<static-values>>.

.The following queries the Twitter API and returns the results
[source,cypher]
----
WITH apoc.static.getAll("twitter") AS twitter
CALL apoc.load.jsonParams(
  twitter.url + "oscon+OR+neo4j+OR+%23oscon+OR+%40neo4j",
  {Authorization:"Bearer "+twitter.bearer},
  null // payload
)
YIELD value
UNWIND value.statuses as status

WITH status, status.user as u, status.entities as e
RETURN status.id, status.text, u.screen_name,
       [t IN e.hashtags | t.text] as tags,
       e.symbols,
       [m IN e.user_mentions | m.screen_name] as mentions,
       [u IN e.urls | u.expanded_url] as urls;
----


[[load-json-examples-import-json]]
=== Import JSON file created by Export JSON procedures

The `apoc.import.json` procedure can be used to import JSON files created by the `apoc.export.json.*` procedures.
This procedure supports the following config parameters:

.Config parameters
[opts=header]
|===
| name | default | description
| unwindBatchSize | `5000` | the batch size of the unwind
| txBatchSize | `5000` | the batch size of the transacttion
| importIdName | `neo4jImportId` | the name of the "id" field into the used for the import it refers to the "id" field into the root object of the json.
| nodePropertyMappings | `{}` | The mapping label/property name/property type for Custom Neo4j types (point date). I.e. { User: { born: 'Point', dateOfBirth: 'Datetime' } }
| relPropertyMappings | `{}` | The mapping rel type/property name/property type for Custom Neo4j types (point date). I.e. { KNOWS: { since: 'Datetime' } }
|===

`nodePropertyMappings` and `relPropertyMappings` support the following Neo4j types:

* Point
* Localdate
* Localtime
* Localdatetime
* Duration
* offsettime
* Zoneddatetime


`all.json` contains a subset of Neo4j's movies graph, and was generated by the <<export-database-json,Export JSON procedure>>.

.all.json
[source,json]
----
include::../data/exportJSON/all.json[leveloffset]
----

We can import this file using `apoc.import.json`.

[source,cypher]
----
CALL apoc.import.json("file:///all.json")
----

.Results
[opts=header]
|===
| file               | source | format | nodes | relationships | properties | time | rows | batchSize | batches | done | data
| "file:///all.json" | "file" | "json" | 3     | 1             | 15         | 105  | 4    | -1        | 0       | TRUE | NULL
|===

[[load-json-examples-post-json]]
=== POST a query to the neo4j.com search API

We can perform a POST request to a JSON endpoint by setting the config parameter `method` to `POST`.
We'll also use the `apoc.convert.toJson` function to construct a JSON payload from a Cypher map.

.The following makes a POST request to neo4j's search API
[source,cypher]
----
CALL apoc.load.jsonParams(
  "https://neo4j.com/docs/search/",
  {method: "POST"},
  apoc.convert.toJson({query: "subquery", version: "4.0"})
)
----


.Results
[options="header"]
|===
| value
| {description: "The CALL {} clause evaluates a subquery that returns some values.", weight: 0.6460227966308594, title: "3.16. CALL {} (subquery) - Chapter 3. Clauses", uri: "https://neo4j.com/docs/cypher-manual/4.0/clauses/call-subquery/"}
| {description: "This section provides examples of queries and Cypher commands that can be used with Neo4j Fabric.", weight: 0.05099273845553398, title: "7.3. Queries - Chapter 7. Fabric", uri: "https://neo4j.com/docs/operations-manual/4.0/fabric/queries/"}
| {description: "WHERE adds constraints to the patterns in a MATCH or OPTIONAL MATCH clause or filters the results of a WITH clause.", weight: 0.03291567042469978, title: "3.6. WHERE - Chapter 3. Clauses", uri: "https://neo4j.com/docs/cypher-manual/4.0/clauses/where/"}
| {description: "This appendix contains the recommended style when writing Cypher queries.", weight: 0.031550146639347076, title: "Appendix A. Cypher styleguide - The Neo4j Cypher Manual v4.0", uri: "https://neo4j.com/docs/cypher-manual/4.0/styleguide/"}
| {description: "This section contains information on all the clauses in the Cypher query language.", weight: 0.02944066934287548, title: "Chapter 3. Clauses - The Neo4j Cypher Manual v4.0", uri: "https://neo4j.com/docs/cypher-manual/4.0/clauses/"}
| {description: "", weight: 0.01821548491716385, title: "2.3. Expressions - Chapter 2. Syntax", uri: "https://neo4j.com/docs/cypher-manual/4.0/syntax/expressions/"}

|===