[[cypher-multiple-statements]]
= Run multiple Statements

[abstract]
--
This section describes procedures that can be used to execute multiple Cypher statements.
--

This procedure runs multiple Cypher statements, separated by a semicolon and new line, and returns summary.
Schema operations are not supported.

This section includes:

* <<cypher-multiple-statements-overview>>
* <<cypher-multiple-statements-examples>>

[[cypher-multiple-statements-overview]]
== Procedure Overview

The supported procedure is described in the table below:

[separator=Â¦,opts=header,cols="1,1m,1m,5"]
|===
include::../../../build/generated-documentation/apoc.cypher.runMany.csv[]
|===

[[cypher-multiple-statements-examples]]
== Examples

//[source,cypher]
//----
//CALL apoc.cypher.runMany('cypher;\nstatements;',{params},[{statistics:true,timeout:10}])
//----


.The following creates nodes and relationships via three separate Cypher statements
[source,cypher]
----
CALL apoc.cypher.runMany('
  CREATE (:Person {name: "Mark"});
  CREATE (:Person {name: "Michael"});
  CREATE (david:Person {name: "David"})
  CREATE (google:Company {name: "Google"})
  CREATE (david)-[:WORKS_FOR]->(google);
  ',
  {})
YIELD row, result
RETURN row, result {.nodesCreated, .propertiesSet, .labelsAdded, .relationshipsCreated};
----

.Results
[options="header"]
|===
| row | result
| -1  | {labelsAdded: 1, relationshipsCreated: 0, nodesCreated: 1, propertiesSet: 1}
| -1  | {labelsAdded: 1, relationshipsCreated: 0, nodesCreated: 1, propertiesSet: 1}
| -1  | {labelsAdded: 2, relationshipsCreated: 1, nodesCreated: 2, propertiesSet: 2}
|===

This procedure also returns a row of output for each Cypher statement that has a return statement.

[NOTE]
====
The `RETURN` statement cannot return graph entities (node, relationships, paths) or we'll get a `transaction closed` exception.
====

.The following creates nodes and relationships via three separate Cypher statements that have return values
[source,cypher]
----
CALL apoc.cypher.runMany('
  CREATE (p:Person {name: "Mark"}) RETURN count(*);
  CREATE (p:Person {name: "Michael"}) RETURN count(*);
  CREATE (david:Person {name: "David"})
  CREATE (google:Company {name: "Google"})
  CREATE (david)-[:WORKS_FOR]->(google)
  RETURN count(*);
  ',
  {})
YIELD row, result
RETURN row, result;
----

.Results
[options="header"]
|===
| row | result
| 0   | {`count(*)`: 1}
| -1  | {constraintsRemoved: 0, indexesRemoved: 0, nodesCreated: 1, rows: 1, propertiesSet: 1, labelsRemoved: 0, relationshipsDeleted: 0, constraintsAdded: 0, nodesDeleted: 0, indexesAdded: 0, labelsAdded: 1, relationshipsCreated: 0, time: 0}
| 0   | {`count(*)`: 1}
| -1  | {constraintsRemoved: 0, indexesRemoved: 0, nodesCreated: 1, rows: 1, propertiesSet: 1, labelsRemoved: 0, relationshipsDeleted: 0, constraintsAdded: 0, nodesDeleted: 0, indexesAdded: 0, labelsAdded: 1, relationshipsCreated: 0, time: 0}
| 0   | {`count(*)`: 1}
| -1  | {constraintsRemoved: 0, indexesRemoved: 0, nodesCreated: 2, rows: 1, propertiesSet: 2, labelsRemoved: 0, relationshipsDeleted: 0, constraintsAdded: 0, nodesDeleted: 0, indexesAdded: 0, labelsAdded: 2, relationshipsCreated: 1, time: 0}
|===

By default, this procedure returns a row of transaction statistics per Cypher statement.
If we don't want these statistics, we can disable them by passing the config `statistics: false`.

.The following creates nodes and relationships via three separate Cypher statements and does not return summary statistics
[source,cypher]
----
CALL apoc.cypher.runMany('
  CREATE (p:Person {name: "Mark"}) RETURN count(*);
  CREATE (p:Person {name: "Michael"}) RETURN count(*);
  CREATE (david:Person {name: "David"})
  CREATE (google:Company {name: "Google"})
  CREATE (david)-[:WORKS_FOR]->(google)
  RETURN count(*);
  ',
  {},
  {statistics: false})
YIELD row, result
RETURN row, result;
----

.Results
[options="header"]
|===
| row | result
| 0   | {`count(*)`: 1}
| 0   | {`count(*)`: 1}
| 0   | {`count(*)`: 1}
|===