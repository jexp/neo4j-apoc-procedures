[[virtual-nodes-rels]]
== Virtual Nodes/Rels

Virtual Nodes and Relationships don't exist in the graph, they are only returned to the UI/user for representing a graph projection.
They can be visualized or processed otherwise.
Please note that they have negative id's.

=== Function Overview

[cols="1m,5"]
|===
| CALL apoc.create.vNode(['Label'], {key:value,...}) YIELD node | returns a virtual node
| apoc.create.vNode(['Label'], {key:value,...}) | function returns a virtual node
| apoc.create.virtual.fromNode(node, [propertyNames]) | function returns a virtual node  built from an existing node with only the requested properties
| CALL apoc.create.vNodes(['Label'], [{key:value,...}]) | returns virtual nodes
| CALL apoc.create.vRelationship(nodeFrom,'KNOWS',{key:value,...}, nodeTo) YIELD rel | returns a virtual relationship
| apoc.create.vRelationship(nodeFrom,'KNOWS',{key:value,...}, nodeTo) | function returns a virtual relationship
| CALL apoc.create.vPattern({_labels:['LabelA'],key:value},'KNOWS',{key:value,...}, {_labels:['LabelB'],key:value}) | returns a virtual pattern
| CALL apoc.create.vPatternFull(['LabelA'],{key:value},'KNOWS',{key:value,...},['LabelB'],{key:value}) | returns a virtual pattern
|===

// * TODO `CALL apoc.create.vGraph([nodes, {_labels:[],... prop:value,...}], [rels,{_from:keyValueFrom,_to:{_label:,_key:,_value:value}, _type:'KNOWS', prop:value,...}],['pk1','Label2:pk2'])

=== Virtual Nodes/Rels Example

.Virtual node and virtual relationship `vNode`, `vRelationship`

.Simple example aggregate Relationships
[source,cypher]
----
MATCH (from:Account)-[:SENT]->(p:Payment)-[:RECEIVED]->(to:Account)
RETURN from, to, apoc.create.vRelationship(from,'PAID',{amount:sum(p.amount)},to) as rel;
----


.Example with virtual node lookups, people grouped by their countries
[source,cypher]
----
MATCH (p:Person) WITH collect(distinct p.country) as countries
WITH [cName IN countries | apoc.create.vNode(['Country'],{name:cName})] as countryNodes
WITH apoc.coll.groupBy(countryNodes,'name') as countries
MATCH (p1:Person)-[:KNOWS]->(p2:Person)
WITH p1.country as cFrom, p2.country as cTo, count(*) as count, countries
RETURN countries[cFrom] as from, countries[cTo] as to, apoc.create.vRelationship(countries[cFrom],'KNOWS',{count:count},countries[cTo]) as rel;
----

That's of course easier with `apoc.nodes.group`.

From a simple dataset

[source,cypher]
----
CREATE(a:Person)-[r:ACTED_IN]->(b:Movie)
----

We can create a virtual copy, adding as attribute `name` the labels value

[source,cypher]
----
MATCH (a)-[r]->(b)
WITH head(labels(a)) AS l, head(labels(b)) AS l2, type(r) AS rel_type, count(*) as count
CALL apoc.create.vNode([l],{name:l}) yield node as a
CALL apoc.create.vNode([l2],{name:l2}) yield node as b
CALL apoc.create.vRelationship(a,rel_type,{count:count},b) yield rel
RETURN *;
----

image::{img}/apoc.create.vRelationshipAndvNode.png[width=800]

[NOTE]
Virtual nodes and virtual relationships have always a negative id

image::{img}/vNodeId.png[width=200]

Virtual nodes can also be built from existing nodes, filtering the properties in order to get a subset of them.
In this case, the Virtual node keeps the id of the original node.

[source,cypher]
----
MATCH (node:Person {name:'neo', age:'42'})
return apoc.create.virtual.fromNode(node, ['name']) as person
----

.Virtual pattern `vPattern`

[source,cypher]
----
CALL apoc.create.vPattern({_labels:['Person'],name:'Mary'},'KNOWS',{since:2012},{_labels:['Person'],name:'Michael'})
----

image::{img}/apoc.create.vPattern.png[width=800]

We can add more labels, just adding them on `_labels`

[source,cypher]
----
CALL apoc.create.vPattern({_labels:['Person', 'Woman'],name:'Mary'},'KNOWS',{since:2012},{_labels:['Person', 'Man'],name:'Michael'})
----

image::{img}/apoc.create.vPatternLabels.png[width=800]

.Virtual pattern full `vPatternFull`

[source,cypher]
----
CALL apoc.create.vPatternFull(['British','Person'],{name:'James', age:28},'KNOWS',{since:2009},['Swedish','Person'],{name:'Daniel', age:30})
----

image::{img}/apoc.create.vPatternFull.png[width=800]

We can create a virtual pattern from an existing one

[source,cypher]
----
CREATE(a:Person {name:'Daniel'})-[r:KNOWS]->(b:Person {name:'John'})
----

From this dataset we can create a virtual pattern

[source,cypher]
----
MATCH (a)-[r]->(b)
WITH head(labels(a)) AS labelA, head(labels(b)) AS labelB, type(r) AS rel_type, a.name AS aName, b.name AS bName
CALL apoc.create.vPatternFull([labelA],{name: aName},rel_type,{since:2009},[labelB],{name: bName}) yield from, rel, to
RETURN *;
----

image::{img}/apoc.create.vPatternFullTwo.png[width=800]

== Nodes collapse

[cols="1m,5"]
|===
| apoc.nodes.collapse([<node>], {config}) | returns the list of nodes merged into a VirtualNode
|===

==== Config:

On `apoc.nodes.collapse` with config properties you can choose from 3 different behavior:

 * "properties": "overwrite" : if there is the same property in more node, in the new one will have the last relationship's/node's property value
 * "properties": "discard" : if there is the same property in more node, the new one will have the first relationship's/node's property value
 * "properties": "combine" : if there is the same property in more node, the new one a value's array with all relationship's/node's values

If properties parameter isn't set relationships properties are `discard`.

 * "mergeVirtualRels: true/false" : give the possibility to merge relationships with same type and direction. (DEFAULT `true`)
 * "selfRel: true/false" : give the possibility to create the self relationship. (DEFAULT `false`)
 * "countMerge: true/false" : give the possibility count all the Nodes/Relationships merged. (DEFAULT `true`)
 * "collapsedLabel: true/false" : give the possibility to add the label `:Collapsed` to the virtualNode. (DEFAULT `false`)

== Nodes collapse example

With this dataset we have:

image::{img}/apoc.nodes.collapse_1.png[width=600]

If we want to collapse the people living in the city to a single node, we pass them to the procedure.

[source,cypher]
----
MATCH (p:Person)-[:LIVES_IN]->(c:City)
WITH c, collect(p) as subgraph
CALL apoc.nodes.collapse(subgraph,{properties:'combine'}) yield from, rel, to
return from, rel, to
----

And get this result:

image::{img}/apoc.nodes.collapse_2.png[width=600]

With this dataset we have:

image::{img}/apoc.nodes.collapse_3.png[width=600]

If we also want to collapse them onto the city itself, we add the city node _first_ to the collection.

[source,cypher]
----
MATCH (p:Person)-[:LIVES_IN]->(c:City)
WITH c, c + collect(p) as subgraph
CALL apoc.nodes.collapse(subgraph) yield from, rel, to
return from, rel, to
----

And get this result:

image::{img}/apoc.nodes.collapse_4.png[width=600]
