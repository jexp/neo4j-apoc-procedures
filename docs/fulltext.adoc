= Full Text Search

Indexes are used for finding nodes in the graph that further operations can then continue from.
Just like in a book where you look at the index to find a section that interest you, and then start reading from there.
A full text index allows you to find occurrences of individual words or phrases across all attributes.

In order to use the full text search feature, we have to first index our data by specifying all the attributes we want to index.
Here we create a full text index called `“locations”` (we will use this name when searching in the index) with our data.

Optionally you can enable tracking changes to the graph on a per index level. To do so, you need to take two actions:

. set `apoc.autoUpdate.enabled=true` in your `neo4j.conf`. With that setting a `TransactionEventHandler` is registered upon startup of your graph database that reflects property changes to the respective fulltext index.
. By default index tracking is done synchronously within the same transaction. Optionally this can be done asynchronous by setting `apoc.autoUpdate.async=true`.
. indexing is started with:

[source,cypher]
----
CALL apoc.index.addAllNodes('locations',{
  Company: ["name", "description"],
  Person:  ["name","address"],
  Address: ["address"]})
----

Creating the index will take a little while since the procedure has to read through the entire database to create the index.

We can now use this index to search for nodes in the database.
The most simple case would be to search across all data for a particular word.

It does not matter which property that word exists in, any node that has that word in any of its indexed properties will be found.

ifdef::env-guide[]

If you enter a name into the form, all occurrences will be found (but limited to 100 results).

++++
<form><div class="node"><div class="form-group">
<label>Word to search for:</label> <input value-for="search" class="form-control">
</div></div></form>
++++

[source,cypher,subs="none,attributes"]
----
CALL apoc.index.search("locations", '<span  value-key="search"></span>')
----

endif::[]

ifndef::env-guide[]

If you use a name in the call, all occurrences will be found (but limited to 100 results).


[source,cypher]
----
CALL apoc.index.search("locations", 'name')
----

endif::[]

== Advanced Search

We can further restrict our search to only searching in a particular attribute.
In order to search for a `Person` with an address in *France*, we use the following.

[source,cypher]
----
CALL apoc.index.search("locations", "Person.address:France")
----

ifdef::env-guide[]

Test it yourself:

++++
<form><div class="node"><div class="form-group">
<label>Label/Type to search for:</label> <input value-for="label" class="form-control">
<label>Attribute to search for:</label> <input value-for="property" class="form-control">
<label>Word to search for:</label> <input value-for="search" class="form-control">
</div></div></form>
++++

[source,cypher,subs="none,attributes"]
----
CALL apoc.index.search("locations", "<span  value-key="label"></span>.<span  value-key="property"></span>:<span value-key="search"></span>")
----

endif::env-guide[]

Now we can search for nodes with a specific property value, and then explore their neighbourhoods visually.

But integrating it with an graph query is so much more powerful.

== Fulltext and Graph Search

We could for instance search for addresses in the database that contain the word "Paris", and then find all companies registered at those addresses:

[source,cypher]
----
CALL apoc.index.search("locations", "Address.address:Paris~") YIELD node AS addr
MATCH (addr)<-[:HAS_ADDRESS]-(company:Company)
RETURN company LIMIT 50
----

The tilde (~) instructs the index search procedure to do a fuzzy match, allowing us to find “Paris” even if the spelling is slightly off.

We might notice that there are addresses that contain the word “Paris” that are not in Paris, France.
For example there might be a Paris Street somewhere.

We can further specify that we want the text to contain both the word Paris, and the word France:

[source,cypher]
----
CALL apoc.index.search("locations", "+Address.address:Paris~ +France~")
YIELD node AS addr
MATCH (addr)<-[:HAS_ADDRESS]-(company:Company)
RETURN company LIMIT 50

----

== Complex Searches

Things start to get interesting when we look at how the different entities in Paris are connected to one another.
We can do that by finding all the entities with addresses in Paris, then creating all pairs of such entities and finding the shortest path between each such pair:

[source,cypher]
----
CALL apoc.index.search("locations", "+Address.address:Paris~ +France~") YIELD node AS addr
MATCH (addr)<-[:HAS_ADDRESS]-(company:Company)
WITH collect(company) AS companies

// create unique pairs
UNWIND companies AS x UNWIND companies AS y
WITH x, y WHERE ID(x) < ID(y)

MATCH path = shortestPath((x)-[*..10]-(y))
RETURN path
----

For more details on the query syntax used in the second parameter of the `search` procedure,
please see http://www.lucenetutorial.com/lucene-query-syntax.html[this Lucene query tutorial]
